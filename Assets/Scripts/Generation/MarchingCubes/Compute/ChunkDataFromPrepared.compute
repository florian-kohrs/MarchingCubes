#pragma kernel ChunkDataFromPrepared
#include "/Includes/MarchTables.compute"
#include "/Includes/BiomVisualizationData.compute"
#include "/Includes/Bioms.compute"
#include "Assets/Scripts/ComputeIncludes/ZipData.compute"

#define PI 3.14159265358979323846
#define SURFACE_LEVEL 0

static const int WAFE_FRONT_SIZE = 32;

static const float rad = 180 / PI;

struct Triangle
{
	float3 vertexC;
	float3 vertexB;
	float3 vertexA;
};

struct ColorData
{
	uint colorAndSteepnessC;
	uint colorAndSteepnessB;
	uint colorAndSteepnessA;
};

RWStructuredBuffer<float> points;
RWStructuredBuffer<int2> triangleLocations;
StructuredBuffer<BiomVisualizationData> biomsViz;

RWStructuredBuffer<Triangle> triangles;
RWStructuredBuffer<Color> colors;

int minSteepness;
int maxSteepness;

float3 anchor;
float spacing;
int numPointsPerAxis;
uint length;

float3 interpolateVerts(float4 v1, float4 v2)
{
	//return v1.xyz + 0.5 * (v2.xyz - v1.xyz);
	float t = (- v1.w) / (v2.w - v1.w);
	return v1.xyz + t * (v2.xyz - v1.xyz);
}

//TODO:Compare speed replacing all ints with uints
int indexFromCoord(int x, int y, int z)
{
	return z * numPointsPerAxis * numPointsPerAxis + y * numPointsPerAxis + x;
}


//int angle(int3 a, int3 b)
//{
//    return acos(dot(a, b)) * 180 / PI
//}

float4 GetHeightDataFrom(int x, int y, int z)
{
	float3 f3 = float3(x, y, z);
	int index = indexFromCoord(x, y, z);

	return float4(anchor + f3 * spacing, points[index]);
}


//check for speed when having 5 times as many threads on z axis one for each triangle -> not better
[numthreads(WAFE_FRONT_SIZE, 1, 1)]
void ChunkDataFromPrepared(uint3 id : SV_DispatchThreadID)
{
	uint threadXID = id.x;
	if (threadXID >= length)
		return;

	int2 buildThis = triangleLocations[id.x];
	int zipedCubeData = buildThis.x;
	int4 data = unzipData(zipedCubeData);
	id = uint3(data.x, data.y, data.z);
	int cubeIndex = data.w;
	int triIndex = buildThis.y;

	// 8 corners of the current cube
	float4 cubeCorners[8] =
	{
		GetHeightDataFrom(id.x, id.y, id.z),
		GetHeightDataFrom(id.x + 1, id.y, id.z),
		GetHeightDataFrom(id.x + 1, id.y, id.z + 1),
		GetHeightDataFrom(id.x, id.y, id.z + 1),
		GetHeightDataFrom(id.x, id.y + 1, id.z),
		GetHeightDataFrom(id.x + 1, id.y + 1, id.z),
		GetHeightDataFrom(id.x + 1, id.y + 1, id.z + 1),
		GetHeightDataFrom(id.x, id.y + 1, id.z + 1)
	};

	float3 pos = cubeCorners[0].xyz;
	int3 biomEvaluationPos = GetBiomEvaluationPosition(pos);
	uint biomIndex = GetClosestBiomIndex(biomEvaluationPos);

	int index = indexFromCoord(id.x, id.y, id.z);

	BiomVisualizationData biom = biomsViz[biomIndex];

	float minDegree = 180;

	int triangulationAtIndex[] = triangulation[cubeIndex];

	// Create triangles for current cube configuration
	

	// Get indices of corner points A and B for each of the three edges
	// of the cube that need to be joined to form the triangle.
	int a0 = cornerIndexAFromEdge[triangulationAtIndex[triIndex]];
	int b0 = cornerIndexBFromEdge[triangulationAtIndex[triIndex]];

	int a1 = cornerIndexAFromEdge[triangulationAtIndex[triIndex + 1]];
	int b1 = cornerIndexBFromEdge[triangulationAtIndex[triIndex + 1]];

	int a2 = cornerIndexAFromEdge[triangulationAtIndex[triIndex + 2]];
	int b2 = cornerIndexBFromEdge[triangulationAtIndex[triIndex + 2]];

	Triangle tri;
	tri.vertexA = interpolateVerts(cubeCorners[a0], cubeCorners[b0]);
	tri.vertexB = interpolateVerts(cubeCorners[a1], cubeCorners[b1]);
	tri.vertexC = interpolateVerts(cubeCorners[a2], cubeCorners[b2]);

	float3 normal = normalize(cross(tri.vertexB - tri.vertexA, tri.vertexC - tri.vertexA));
	float3 middlePoint = (tri.vertexA + tri.vertexB + tri.vertexC) / 3;
	float angleFromCenter = 180 - (acos(dot(normal, normalize(middlePoint))) * rad);

	float invLerp = (angleFromCenter - minSteepness) / (maxSteepness - minSteepness);
	if (invLerp < 0)
		invLerp = 0;
	else if (invLerp > 1)
		invLerp = 1;

	uint3 flatColor = biom.flatRGB;
	uint3 steepColor = biom.steepRGB;

	int colorR = (invLerp * steepColor.x + (1 - invLerp) * flatColor.x);
	int colorG = (invLerp * steepColor.y + (1 - invLerp) * flatColor.y);
	int colorB = (invLerp * steepColor.z + (1 - invLerp) * flatColor.z);

	int finalColor = zipData((int)angleFromCenter, colorB, colorG, colorR);

	ColorData color;
	color.colorAndSteepnessA = finalColor;
	color.colorAndSteepnessB = finalColor;
	color.colorAndSteepnessC = finalColor;

	colors[threadXID] = color;
	triangles[threadXID] = tri;
}
